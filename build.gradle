plugins {
  id("application")
  id("idea")
  id("jacoco")
  id("java-library")
  id("com.github.ben-manes.versions") version "0.28.0"
  id("org.jetbrains.gradle.plugin.idea-ext") version "0.7"
  id("org.sonarqube") version "3.0"
  id("org.springframework.boot") version "2.3.1.RELEASE"
}

def VERSIONS = [
  assertj   : "3.16.1",
  cucumber  : "6.2.2",
  junit     : "5.7.0-M1",
  gradle    : "6.5.1",
  springBoot: "2.3.1.RELEASE"
]

group "com.hiberbee"
version "1.0.0"

application {
  mainClassName = "${project.group}.Application" as String
}

springBoot {
  buildInfo()
}

class CucumberFeature extends JavaExec {

  @Input
  void withTags(String... tags) {
    tags.each {
      args += ["--include-tag", it]
    }
  }

  @Input
  void withoutTags(String... tags) {
    tags.each {
      args += ["--exclude-tag", it]
    }
  }

  @Input
  void withPackage(String packageName = "features") {
    args += ["--select-package", packageName]
  }

  @Input
  void withProperties(Map<String, Object> properties) {
    properties.each {
      jvmArgs += "-D${it.key}=${it.value}"
    }
  }

  CucumberFeature() {
    group "cucumber"
    dependsOn project.tasks.getByName("testClasses"), project.tasks.getByName("processResources")
    outputs.dir "${project.buildDir}/reports"
    main "org.junit.platform.console.ConsoleLauncher"
    classpath project.
      convention.
      getPlugin(JavaPluginConvention.class).
      getSourceSets().
      findByName("test").
      runtimeClasspath
    args "--include-engine", "cucumber", "--reports-dir", "${project.buildDir}/reports"
    finalizedBy project.tasks.getByName("jacocoTestReport")
  }
}

tasks {
  compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.fork = true
    options.warnings = false
    options.incremental = true
    dependsOn tasks.getByName("processResources")
  }
  dependencyUpdates {
    checkConstraints true
  }
  idea {
    module {
      outputDir project.buildDir
      downloadJavadoc true
      downloadSources true
      inheritOutputDirs true
      excludeDirs += files(".gradle", ".idea", ".scannerwork", "gradle")
      generatedSourceDirs += files(
        "${project.buildDir}/generated/sources/annotationProcessor/java/main",
        "${project.buildDir}/generated/sources/annotationProcessor/java/test"
      )
    }
  }
  sonarqube {
    properties {
      property "sonar.projectName", project.name
      property "sonar.projectKey", project.name
    }
  }
  wrapper {
    gradleVersion VERSIONS.gradle
    distributionType Wrapper.DistributionType.ALL
  }
  jacocoTestReport {
    reports {
      xml.enabled false
      csv.enabled false
      html.enabled true
      html.destination file("${project.buildDir}/reports/jacoco")
    }
  }
  test {
    useJUnitPlatform {
      failFast true
    }
    scanForTestClasses false
    jacoco {
      enabled true
      dumpOnExit true
    }
    finalizedBy tasks.getByName("jacocoTestReport")
  }
}

tasks.register("allFeatures", CucumberFeature) {
  it.withPackage("features")
}

repositories {
  mavenCentral()
}

dependencies {
  implementation platform("org.springframework.boot:spring-boot-dependencies:$VERSIONS.springBoot")
  implementation platform("io.cucumber:cucumber-core:$VERSIONS.cucumber")
  testImplementation platform("org.junit:junit-bom:$VERSIONS.junit")
  testRuntime platform("org.junit:junit-bom:$VERSIONS.junit")

  implementation("org.springframework.boot:spring-boot-starter-web")
  implementation("io.cucumber:cucumber-core")
  implementation("io.cucumber:cucumber-java")
  implementation("io.cucumber:cucumber-spring")
  implementation("org.assertj:assertj-core:$VERSIONS.assertj")
  testImplementation("org.springframework.boot:spring-boot-starter-test")
  testImplementation("org.junit.jupiter:junit-jupiter-api")
  testImplementation("io.cucumber:cucumber-junit-platform-engine")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
  testRuntimeOnly("org.junit.platform:junit-platform-console")
  testRuntimeOnly("org.junit.platform:junit-platform-runner")
}
